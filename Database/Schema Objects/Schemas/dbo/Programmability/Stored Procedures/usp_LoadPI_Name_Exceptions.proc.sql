-- =============================================
-- Author:		Ken Taylor
-- Create date: November 26, 2014
-- Description:	Load PI_Name_Exceptions table with any 241 
-- employees that do not already have a PI_Match.
-- Usage:
/*
	EXEC [dbo].[usp_LoadPI_Name_Exceptions] @IsDebug = 0
*/
-- Modifications:
-- 2015-03-31 by kjt:  Removed any [AD419] database specific references so that this sproc can be used with other databases
--		such as [AD419_2014], etc. 
-- 2015-11-02 by kjt: Added column names to insert statement because I added FirstName, which is populated by a subsequent script.
-- 2016-03-24 by kjt: Added logic to populate middle initial using UCD Mail ID from PPS.
-- 2016-08-15 by kjt: Added logic for also removing space between VAN and second portion of last name, and '. <space> like St. Clair.
-- 2016-08-17 by kjt: Added Usage example.
-- =============================================
CREATE PROCEDURE [dbo].[usp_LoadPI_Name_Exceptions] 
	@IsDebug bit = 0  --Set this to 1 to display results generated by procedure only.  No changes will be made to the tables.
AS
BEGIN
	-- SET NOCOUNT ON added to prevent extra result sets from
	-- interfering with SELECT statements.
	SET NOCOUNT ON;

  DECLARE @Table TABLE (
	OrgR varchar(4), 
	EmployeeName varchar(200), 
	EID varchar(50), 
	PI varchar(200), 
	LastName varchar(100), 
	FirstMiddle varchar(50),
	FirstInitial varchar(5),
	FirstName varchar(50),
	MiddleInitial varchar(5),
	FirstPortionOfLastName varchar(50),
	ModifiedPIName varchar(200),
	ModifiedPIName2 varchar(200)) 

  -- Note: Cursor automatically takes care of removing hyphens from names with to match names with
  -- missing hyphens like O'Malley to OMalley.
  DECLARE myCursor CURSOR FOR SELECT 
	   REPLACE(SUBSTRING(PI, 0, CHARINDEX(', ',PI, 1) -1), '''', '') + '%' LastName
  FROM [dbo].[PI_Match]
  where PI_MATCH IS NULL

  -- Loop through each of the names with no corresponding PI match value and insert into the table variable,
  -- setting the Lastname and First(Name)Middle(Initial) fields. 
  DECLARE @LastName varchar(100)
  OPEN myCursor
  FETCH NEXT FROM myCursor INTO @LastName
  WHILE @@FETCH_STATUS <> -1
  BEGIN
	INSERT INTO @Table (OrgR, EmployeeName, EID, PI, LastName, FirstMiddle)
	SELECT  [OrgR]
      ,REPLACE([EmployeeName], '(CE PI) ', '') EmployeeName
      ,[EID]
      ,REPLACE([PI], '(CE PI) ', '') [PI]
	  , SUBSTRING(REPLACE([EmployeeName], '(CE PI) ', ''), 0, CHARINDEX(',', REPLACE([EmployeeName], '(CE PI) ', ''), 1)) LastName
	  , SUBSTRING(EmployeeName, CHARINDEX(',', EmployeeName, 1) + 1, 75) FirstMiddle

  FROM [dbo].[PI_Names]
  Where employeename like @LastName OR 
	REPLACE(EmployeeName, 'VAN ', 'VAN') like @LastName OR
	REPLACE(EmployeeName, 'ST ', 'ST') like @LastName 

	FETCH NEXT FROM myCursor INTO @LastName
  END
  CLOSE myCursor
  DEALLOCATE myCUrsor
  
  IF @IsDebug = 1
	BEGIN
	  -- Add some dummy entries for testing names like MC/MAC and JOHNS-HOPKINS:
	  INSERT INTO @Table (OrgR, EmployeeName, EID, PI)
	  VALUES 
	  ('ABCD', UPPER('Mc Nalley,Sam P'), '0123456789', UPPER('Mc Nalley, S P')), 
	  ('ABCD', UPPER('Johns-Hopkins,Stuart W'), '0123456789', UPPER('Johns-Hopkins, S P'))

	  UPDATE @Table SET LastName = SUBSTRING(EmployeeName, 0, CHARINDEX(',', EmployeeName, 0)), 
	  FirstMiddle = SUBSTRING(EmployeeName, CHARINDEX(',', EmployeeName, 0) + 1, 75)
	  WHERE LastName IS NULL
	END

  -- Set the first initial:
  UPDATE @Table
  SET FirstInitial = SUBSTRING(FirstMiddle, 1 ,1)

  -- Set the middle initial:
  -- Use the first initial of the first/middle, i.e., "John P", and the middle initial unless there's a hyphen in the first name, i.e., "C-Y Carol".
  -- If there's a hyphen in the "firstname" then assume it's first initial<dash>middle initial, followed by a nick name, as in
  -- "C-Y Carol", where the first initial is "C", and the middle initial is "Y".
  UPDATE @Table 
  SET [MiddleInitial] = CASE WHEN CHARINDEX('-', FirstMiddle, 1) > 0 THEN SUBSTRING(FirstMiddle,  CHARINDEX('-', FirstMiddle, 1) + 1, 1) 
	   ELSE 
			CASE WHEN CHARINDEX(' ', FirstMiddle, 1 ) > 0 THEN SUBSTRING(FirstMiddle, CHARINDEX(' ', FirstMiddle, 1) +1, 1) ELSE NULL END
	   END
  
  /*
  Things to consider:
  1. "Mc/Mac Whatever" should return "McWhatever", etc.
  2. "Johns-Hopkins" should return "Johns"
  3. "OTEIZA DE FRAGA" should return "OTEIZA"
  */
  -- Use first portion of last name if multiple names separated by spaces, i.e. "OTEIZA DE FRAGA":
  UPDATE @Table 
  SET FirstPortionOfLastName = 
	CASE WHEN CHARINDEX(' ', LastName, 1) > 0 THEN 
		CASE WHEN -- starts with Mc, Mac, Van or Von 
			(CHARINDEX('MAC ', LastName, 1) > 0 OR
			CHARINDEX('MC ', LastName, 1) > 0 OR
			CHARINDEX('VAN ', LastName, 1) > 0 OR
			CHARINDEX('VON ', LastName, 1) > 0) THEN
				-- Remove space
				REPLACE(LastName, ' ', '')			
			ELSE -- This must be an other type of multi-part last name
				-- Take first part of last name and call it a day
				SUBSTRING(LastName, 0, CHARINDEX(' ', LastName, 1))
		END 
		WHEN CHARINDEX('-', LastName, 1) > 0 THEN 
			-- Take first part of last name and call it a day
			SUBSTRING(LastName, 0, CHARINDEX('-', LastName, 1))
		ELSE -- Just use the last name the way it is.
			LastName
	END 

  -- Set the ModifiedPIName:
  UPDATE @Table 
  SET ModifiedPIName = CASE WHEN MiddleInitial IS NULL THEN FirstPortionOfLastName + ', ' + FirstInitial ELSE
	  FirstPortionOfLastName + ', ' + FirstInitial  + ' ' + MiddleInitial END

 -- Set first names where there's both a first name and middle initial:
  UPDATE @Table 
  SET FirstName = SUBSTRING(FirstMiddle, 0, CHARINDEX(' ', FirstMiddle, 0) )

  -- Set first names where there's no middle initial:
  UPDATE @Table 
  SET FirstName = FirstMiddle where MiddleInitial IS NULL

  -- Set first names which are now blank because they are a hyphonated first name:
  UPDATE @Table
  SET FirstName = CASE WHEN CHARINDEX(' ', FirstMiddle, 0) = 0 THEN FirstMiddle END
  WHERE FirstName = ''

  -- This will update the PI_Name_Exceptions' middle initial and firstMiddle using the UCD Mail ID from PPS:
		UPDATE @Table
		SET FirstMiddle = t2.FirstMiddle, MiddleInitial = t2.MI 
		FROM
		@Table t1
		INNER JOIN
		(
			SELECT t2.EmployeeID, t2.FullName, t2.FirstName, t2.MI, 
			CASE WHEN MI IS NOT NULL THEN t2.FirstName + ' ' + MI 
			ELSE t2.FirstName END AS FirstMiddle 
			FROM
			@Table t1
			INNER JOIN 
			( 
				select DISTINCT EmployeeID, FullName, FirstName, CASE WHEN LEN(REPLACE(UCDMailID, replace(LastName, ' ', ''), '')) = 2  THEN UPPER(SUBSTRING(REPLACE(UCDMailID, replace(LastName, ' ', ''), ''), 2, 1))
				ELSE NULL END MI, UCDMailID 
				FROM PPSDatamart.dbo.Persons 
				WHERE EmployeeID IN (
					select EID from @Table where MiddleInitial is NULL
				) 
			) T2 ON t1.EID = t2.EmployeeID
		) t2 ON t1.EID = t2.EmployeeID


	-- Set the ModifiedPiName2 for matching:
	UPDATE @Table SET ModifiedPiName2 = LastName + ', ' + FirstInitial + ' ' + MiddleInitial
	WHERE ModifiedPIName NOT LIKE LastName + ', ' + FirstInitial + ' ' + MiddleInitial

  IF @IsDebug = 1
	BEGIN
		SELECT  DISTINCT 
		   [OrgR]
		  ,[EmployeeName]
		  ,[EID]
		  ,[PI]
		  ,[LastName]
		  ,[FirstMiddle]
		  ,[FirstInitial]
		  ,[FirstName]
		  ,[MiddleInitial]
		  ,[FirstPortionOfLastName]
		  ,[ModifiedPIName]
		  ,[ModifiedPIName2]
  FROM @Table
  ORDER BY [OrgR], [EmployeeName]
	END
  ELSE
	BEGIN
		TRUNCATE TABLE dbo.PI_Name_Exceptions

		--Insert INTO [DBO].[PI_Name_Exceptions] table:
		INSERT INTO [DBO].[PI_Name_Exceptions] (
		   [OrgR]
		  ,[EmployeeName]
		  ,[EID]
		  ,[PI]
		  ,[LastName]
		  ,[FirstMiddle]
		  ,[FirstInitial]
		  ,[FirstName]
		  ,[MiddleInitial]
		  ,[FirstPortionOfLastName]
		  ,[ModifiedPIName]
		  ,[ModifiedPIName2])
		SELECT DISTINCT * FROM @Table ORDER BY PI , OrgR 

		SELECT  
		   [OrgR]
		  ,[EmployeeName]
		  ,[EID]
		  ,[PI]
		  ,[LastName]
		  ,[FirstMiddle]
		  ,[FirstInitial]
		  ,[FirstName]
		  ,[MiddleInitial]
		  ,[FirstPortionOfLastName]
		  ,[ModifiedPIName]
		  ,[ModifiedPIName2]
	  FROM [dbo].[PI_Name_Exceptions]
	END
    
	SET NOCOUNT OFF;
END