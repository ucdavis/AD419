-- =============================================
-- Author:		Ken Taylor
-- Create date: September 7, 2012
-- Description:	Update the FISDataMart ARC_Codes table 
-- from the DaFIS FINANCE.ANNUAL_REPORT_CODE table.
-- Usage:
/*
USE [FISDataMart]
GO

EXEC [dbo].[usp_DownloadArc_Codes]
@TableName = 'ARC_Codes',
@IsDebug = 1

*/
--
-- NOTE: Run this prior to updating the isAES flag based on AD419 Schedule C provided by Steve Pesis.
--
-- Modifications:
--	20160128 by kjt: Added 2 additional columns: ARC_CATEGORY_CD, and ARC_SUB_CATEGORY_CD.
--	20160331 by kjt: Added VM ARCs to exclusion list.
-- =============================================
CREATE PROCEDURE [dbo].[usp_DownloadArc_Codes]
	@TableName varchar(255) = 'ARC_Codes', -- Optional ARC codes table name if different from default. 
	@IsDebug bit = 0 -- Set to 1 to print SQL generated by script only.
AS
BEGIN

	DECLARE @TSQL varchar(MAX) = '
	MERGE [FISDataMart].[dbo].[' + @TableName + '] AS ARC_Codes
	USING
	(
		SELECT ARC_Cd, ARC_Name, OP_Func_Name, DS_Last_Update_Date, ARC_Category_Cd, ARC_Sub_Category_Cd,
			CASE WHEN ARC_Category_Cd = ''AEX'' AND 
				ARC_Cd NOT IN (
				''440269'', 
				''440275'', 
				''440291'',
				''440292'', 
				''440293'', 
				''440294'', 
				''440295'', 
				''440296'', 
				''440297'',
				''440298'',
				''440299'') THEN 1
				ELSE 0 
			END AS isAES
		FROM OPENQUERY(FIS_DS, ''
			SELECT 
				ANNUAL_REPORT_CODE AS ARC_Cd, 
				ANNUAL_REPORT_CODE_NAME AS ARC_Name, 
				OP_FUNCTION_NAME AS OP_Func_Name, 
				DS_LAST_UPDATE_DATE AS DS_Last_Update_Date,
				ANNUAL_RPT_CATEGORY_CD AS ARC_Category_Cd,
				ANNUAL_RPT_SUB_CATEGORY_CD AS ARC_Sub_Category_Cd 
			FROM FINANCE.ANNUAL_REPORT_CODE
			WHERE UPPER(ANNUAL_REPORT_CODE_NAME) NOT LIKE ''''%DO NOT USE%''''
			'')
		) FIS_DS_ARC_CODES on ARC_Codes.ARC_Cd = FIS_DS_ARC_CODES.ARC_Cd
		WHEN MATCHED THEN UPDATE set
			ARC_Codes.ARC_Name = FIS_DS_ARC_CODES.ARC_Name,
			ARC_Codes.OP_Func_Name = FIS_DS_ARC_CODES.OP_Func_Name,
			ARC_Codes.DS_Last_Update_Date = FIS_DS_ARC_CODES.DS_Last_Update_Date,
			ARC_Codes.ARC_Category_Cd = FIS_DS_ARC_CODES.ARC_Category_Cd,
			ARC_Codes.ARC_Sub_Category_Cd = FIS_DS_ARC_CODES.ARC_Sub_Category_Cd,
			ARC_Codes.isAES = FIS_DS_ARC_CODES.isAES
		WHEN NOT MATCHED BY TARGET THEN INSERT VALUES (
			ARC_Cd,
			ARC_Name,
			OP_Func_Name,
			DS_Last_Update_Date,
			ARC_Category_Cd,
			ARC_Sub_Category_Cd,
			isAES
		 )
--	WHEN NOT MATCHED BY SOURCE THEN DELETE
	;
'
IF @IsDebug = 1
	BEGIN
		SET NOCOUNT ON;
		PRINT @TSQL
		SET NOCOUNT OFF;
	END
ELSE
	EXEC(@TSQL)

END